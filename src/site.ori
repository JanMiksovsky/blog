// Overall site definition for the whole blog
{
  // Apply to template to post pages, and create a page for each year
  years = map(data.ori, (yearPosts, yearSlash) => {
    index.html = yearPage.ori(yearPosts, slash/remove(yearSlash))
    …map(yearPosts, postPage.ori)
  })

  // The recent feed is the 10 most recent posts
  recent = deepTake(data.ori, 10)
  feed = feed.ori(recent)

  // Main public area -- this is the part that gets built and deployed
  public = {

    // Merge in the client folder
    …client

    // About Us and Contact pages
    about.html = page.ori(mdHtml(about.md))
    contact.html = page.ori(mdHtml(contact.md))

    // Include the `images` folder
    images

    // Top-level index page
    index.html = index.ori(recent)

    posts = {
      // Add an index page to the overall posts archive.
      index.html = archivePage.ori(keys(data.ori))

      // Add in posts for all the years
      …years
    }

    // Create preview images for posts
    previews = map(data.ori, =map(_, {
      key: (item) => item/previewSlug
      value: preview.ori
    }))

    // Feeds
    feed.json = json(feed)
    rss.xml = rss(feed, { feed_url: "https://jan.miksovsky.com/rss.xml" })
  }
}