{
  processed = posts
    → @deepMapFn(@mdHtml)
    → @mapFn((yearPosts, year) => @map(
      yearPosts
      (post, filename) => postData.js(post, filename, year)
    ))
    → @deepMapFn({ key: (post) => post/slug })
    → (slugs) => @reverse(slugs, { deep: @true })

  // Apply to template to post pages, and create a page for each year.
  years = @map(processed, (yearPosts, year) => @merge(
    {
      index.html = yearPage.ori(yearPosts, year)
    }
    @map(yearPosts, postPage.ori)
  ))

  // The recent feed is the 10 most recent posts.
  recent = @deepTake(processed, 10)
  feed = feed.ori(recent)

  // Main site
  public = @merge(client, {

    // About Us and Contact pages
    about.html = page.ori(@mdHtml(about.md))
    contact.html = page.ori(@mdHtml(contact.md))

    images

    // Top-level index page
    index.html = index.ori(recent)

    // Add an index page to the overall posts archive.
    posts = @merge(years, {
      index.html = archivePage.ori(@keys(processed))
    })

    feed.json = @json(feed)
    rss.xml = @rss(feed)
    sitemap.xml = @sitemap(public, "https://jan.miksovsky.com")
  })
}