{
  bodies = @tree/map({ deep: @true, extensions: "mdâ†’html", valueFn: @mdHtml })(posts)

  data = @tree/map({ deep: @true, valueFn: =postData.js(_, @key) })(bodies)

  slugKeys = @tree/map({ deep: @true, keyFn: =@or(_/slug, @key) })(data)

  reversed = @tree/reverse(slugKeys, { deep: @true })

  formattedPosts = @tree/map({ deep: @true, valueFn: post.orit })(reversed)

  postPages = @tree/map({ deep: @true, valueFn: page.orit })(formattedPosts)

  # Add an index page to each year folder.
  years = @tree/map(
    =@tree/merge(_, @scope/set({
      index.html = yearPage.orit({ posts: formattedPosts(@key), title: @key })
    }))
  )(postPages)

  # The recent feed is the 10 most recent posts.
  recent = @tree/valuesDeep(takeDeep.js(reversed, 10))
  
  # The feed items are the recent posts with just the necessary feed data.
  feedItems = @tree/map({ deep: @true, valueFn: feedItem.js })(recent)
  feed = @scope/set(feed.yaml, { _: feedItems }, @scope/get())

  # Main site
  public = @tree/merge(client, {

    # Add an index page to the overall posts archive.
    posts = @tree/merge(years, {
      index.html = archivePage.orit(@tree/keys(reversed))
    })

    # Top-level index page
    index.html = index.orit(recent)

    # Contact page
    contact.html = page.orit(@mdHtml(contact.md))
    about.html = page.orit(@mdHtml(about.md))

    feed.json = @json(feed)
    rss.xml = @rss(feed)

    sitemap.xml = @tree/sitemap(public, "https://jan.miksovsky.com")
  })
}