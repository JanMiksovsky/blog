{
  bodies = @tree/map(posts, { deep: @true, extensions: "mdâ†’html", valueMap: @mdHtml })

  data = @tree/map(bodies, { deep: @true, valueMap: postData.js })

  slugKeys = @tree/map(data, { deep: @true, keyMap: =_/slug })

  reversed = @tree/reverse(slugKeys, { deep: @true })

  formattedPosts = @tree/map(reversed, { deep: @true, valueMap: post.orit })

  postPages = @tree/map(formattedPosts, { deep: @true, valueMap: page.orit })

  # Add an index page to each year folder.
  years = @tree/map(
    postPages,
    =@tree/merge(_, @scope/set({
      index.html = yearPage.orit({ posts: formattedPosts(@key), title: @key })
    }))
  )

  # The recent feed is the 10 most recent posts.
  recent = @tree/valuesDeep(takeDeep.js(reversed, 10))
  
  # The feed items are the recent posts with just the necessary feed data.
  # HACK: Shouldn't need to apply @tree/plain, something above is turning
  # document objects into trees.
  feedItems = @tree/map(recent, =feedItem.js(@tree/plain(_)))
  # Evaluate feed.yaml, which will read feedItems above.
  feed = feed.yaml/

  # Main site
  public = @tree/merge(client, {

    # Add an index page to the overall posts archive.
    posts = @tree/merge(years, {
      index.html = archivePage.orit(@tree/keys(reversed))
    })

    # Top-level index page
    index.html = index.orit(recent)

    # About Us and Contact pages
    about.html = page.orit(@mdHtml(about.md))
    contact.html = page.orit(@mdHtml(contact.md))

    feed.json = @json(feed)
    rss.xml = @rss(feed)

    sitemap.xml = @tree/sitemap(public, "https://jan.miksovsky.com")
  })
}