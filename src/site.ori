// Overall site definition for the whole blog
{
  // Main blog post pipeline turns markdown into objects with all the data
  // needed for rendering in various ways
  processed = posts
    → =tree:map(_, (yearPosts, yearSlash) =>
        tree:map(yearPosts, {
          key: (post, fileName) => `${ node:path/basename(site:slug(fileName), ".md") }.html`
          value: (post, fileName) => postData.js(
            post
            origami:slash/remove(fileName)
            origami:slash/remove(yearSlash)
          )
        })
    )
    → tree:deepReverse

  // Apply to template to post pages, and create a page for each year
  years = tree:map(processed, (yearPosts, yearSlash) => {
    index.html = yearPage.ori(yearPosts, origami:slash/remove(yearSlash))
    …tree:map(yearPosts, postPage.ori)
  })

  // The recent feed is the 10 most recent posts
  recent = tree:deepTake(processed, 10)
  feed = feed.ori(recent)

  // Main public area -- this is the part that gets built and deployed
  public = {

    // Merge in the client folder
    …client

    // About Us and Contact pages
    about.html = page.ori(text:mdHtml(about.md))
    contact.html = page.ori(text:mdHtml(contact.md))

    // Include the `images` folder
    images

    // Top-level index page
    index.html = index.ori(recent)

    posts = {
      // Add an index page to the overall posts archive.
      index.html = archivePage.ori(tree:keys(processed))

      // Add in posts for all the years
      …years
    }

    // Create preview images for posts
    previews = tree:map(processed, =tree:map(_, {
      key: (item) => item/previewSlug
      value: preview.ori
    }))

    // Feeds
    feed.json = origami:json(feed)
    rss.xml = site:rss(feed, { feed_url: "https://jan.miksovsky.com/rss.xml" })
  }
}